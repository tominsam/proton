apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.1'

    defaultConfig {
        applicationId 'org.movieos.proton'
        minSdkVersion 21
        targetSdkVersion 24
        versionCode 5
        versionName '0.2'
        resConfigs "en"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    signingConfigs {
        release
    }

    buildTypes {
        debug {
            setApplicationIdSuffix('debug')
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
    }
}

// load signing information based on gradle.properties
// http://www.thoughtworks.com/insights/blog/signing-open-source-android-apps-without-disclosing-passwords
def props = new Properties()
props.load(new FileInputStream(rootProject.file("release.properties")))
android.signingConfigs.release.keyAlias = props.keyAlias
android.signingConfigs.release.storeFile = file(props.keyStore)
android.signingConfigs.release.storePassword = props.keyStorePassword
android.signingConfigs.release.keyPassword = props.keyAliasPassword

dependencies {
    //compile 'com.android.support:support-core-ui:24.2.0'
    compile 'com.android.support:percent:24.2.0'
    compile 'com.android.support:appcompat-v7:24.2.0'
    compile 'com.android.support:recyclerview-v7:24.2.0'
    compile 'com.google.firebase:firebase-core:9.4.0'
    compile 'com.google.firebase:firebase-crash:9.4.0'
    compile 'com.google.firebase:firebase-analytics:9.4.0'
    compile 'com.google.code.gson:gson:2.5'
    retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:2.3.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha7'
}

// must be at bottom
apply plugin: 'com.google.gms.google-services'
